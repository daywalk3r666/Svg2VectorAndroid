group 'geekyappz'
version '1.0.5'

apply plugin: 'java'

repositories {
    maven { url 'https://maven.google.com' }
    mavenCentral()
    jcenter()
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Implementation-Title': 'Svg2VectorAndroid',
                'Implementation-Version': version,
                'Main-Class': 'com.vector.svg2vectorandroid.Runner'
    }
    archiveBaseName = project.name
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    with jar
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '5.9.2'
    implementation 'com.android.tools:sdk-common:30.4.1'
    implementation 'com.android.tools:common:30.4.1'
}
